" =============================================================================
"      FileName: _vimrc
"          Desc:
"        Author: Marslo
"       Version: 0.0.9
"    LastChange: 2013-08-05 21:06:46
"       History: 0.0.3: Add the Autoload and Fast Edit difference between win32 and non-win32
"                0.0.4: Add CheckRubySyntax() function for checking and run ruby script
"                0.0.5: Add the function of highlight txt file
"                       Add the function of Automatic paire signs (), [], {}, '', etc
"                0.0.6: Add bundle plugin for manager plugins automatic
"                0.0.7: Add pyflakes plugin for python syntax checking
"                0.0.8: Remap the shortcut key: Ctrl-A/Ctrl-E/F1
"                0.0.9: Add the function about remove a paird parentheses, brackets, braces
" =============================================================================

source $VIMRUNTIME/vimrc_example.vim
source $VIMRUNTIME/mswin.vim
behave mswin

let g:ruby_path=$RUBY_BIN
let g:solarized_termcolors=256

" Remove the Welcome interface
" set shortmess=atI

" ====================================== For Property =====================================
" Set mapleader
let mapleader=","
let g:mapleader=","

set iskeyword+=.
set autochdir

" Tags
set tags=tags
set tags+=c:\Marslo\Job\Summa\TEX\SVN\netact-mpp-lab-scripts\.tags
autocmd BufEnter c:\Marslo\Job\Summa\TEX\SVN\netact-mpp-lab-scripts\* :setlocal tags+=c:\Marslo\Job\Summa\TEX\SVN\netact-mpp-lab-scripts\.tags

" Code format
set fileencodings=utf8,ucs-bom,gbk,cp936,gb2312,gb18030
set termencoding=utf-8
set encoding=utf8
set fileencoding=utf8
let &termencoding=&encoding

" Mouse Settings
" set mouse=a
set selection=exclusive
set selectmode=mouse,key
set scrolloff=3

set diffexpr=MyDiff()
func! MyDiff()
    let opt = '-a --binary '
    if &diffopt =~ 'icase' | let opt = opt . '-i ' | endif
    if &diffopt =~ 'iwhite' | let opt = opt . '-b ' | endif
    let arg1 = v:fname_in
    if arg1 =~ ' ' | let arg1 = '"' . arg1 . '"' | endif
    let arg2 = v:fname_new
    if arg2 =~ ' ' | let arg2 = '"' . arg2 . '"' | endif
    let mp = &makeprg
    let mp = &makeprg
    let arg3 = v:fname_out
    if arg3 =~ ' ' | let arg3 = '"' . arg3 . '"' | endif
    let eq = ''
    if $VIMRUNTIME =~ ' '
        if &sh =~ '\<cmd'
            let cmd = '""' . $VIMRUNTIME . '\diff"'
            let eq = '"'
        else
            let cmd = substitute($VIMRUNTIME, ' ', '" ', '') . '\diff"'
        endif
    else
        let cmd = $VIMRUNTIME . '\diff'
    endif
    silent execute '!' . cmd . ' ' . opt . arg1 . ' ' . arg2 . ' > ' . arg3 . eq
endfunc

" Vim Bundle
set nocompatible
filetype off
set rtp+=$VIM/vimfiles/bundle/vundle
call vundle#rc()

" Plugins
Bundle 'gmarik/vundle'
Bundle 'Yggdroot/indentLine'
Bundle 'kien/ctrlp.vim.git'
Bundle 'sjl/gundo.vim.git'
Bundle 'majutsushi/tagbar'
Bundle 'AuthorInfo'
Bundle 'Conque-Shell'
Bundle 'mru.vim'
Bundle 'python_fold'
" Bundle 'Tagbar'
Bundle 'taglist.vim'
Bundle 'TeTrIs.vim'
Bundle 'winmanager'
Bundle 'matrix.vim--Yang'
Bundle 'pyflakes.vim'
" Bundle 'EnhCommentify.vim'
" Bundle 'snipMate'
" Bundle 'xolox/vim-shell'
" Bundle 'xolox/vim-misc'
" Bundle 'xolox/vim-easytags'

" Colors
Bundle 'txt.vim'
Bundle 'css.vim'
Bundle 'hail2u/vim-css3-syntax'

filetype plugin indent on
" nmap bi :BundleInstall<CR>

" ====================================== For Programming =====================================
" Open the current file path by cmd
func! OpenCMD()
    if has('win32') || has('win95') || has('win64')
        " let com = '!cmd /c start C:\Marslo\Tools\Softwares\Data\System\CMD\Console2\Console.exe -d "'. expand('%:p:h') . '"'
        " let com = '!cmd /c start cd '. expand('%:p:h')
        " let com = '!cmd /c start "C:\Program Files\JPSoft\TCCLE13\tcc.exe" /c "' . expand('%:p:h') .'"'
        " let com = '!cmd /c start C:\WINDOWS\system32\WindowsPowerShell\v1.0\powershell.exe -NoExit -Command "Set-Location ' . expand ('%:p:h') . '"'
        let com1 = '!cmd /c start C:\WINDOWS\system32\WindowsPowerShell\v1.0\powershell.exe -NoExit -Command '
        let com2 = '"Set-Location ' . "'" . expand ('%:p:h') ."'" . '"'
        let com = com1 . com2
    else
        let com = '!/usr/bin/gnome-terminal --working-directory=' . expand('%:p:h')
    endif
    silent execute com
endfunc
nmap cmd :call OpenCMD()<CR>

func! OpenFoler()
    if has('win32') || has('win95') || has('win64')
        let folderpath = expand('%:p:h')
    endif
    silent execute '!C:\Windows\explorer.exe "' . folderpath . '"'
endfunc
map <M-o> :call OpenFoler()<CR>

" Run pylint
func! PylintCheck()
    let mp = &makeprg
    let ef = &errorformat
    let exeFile = '"' . expand("%:t") . '"'
    setlocal makeprg=pylint\ --reports=n\ --output-format=parseable
    set efm=%A%f:%l:\ [%t%.%#]\ %m,%Z%p^^,%-C%.%#
    silent make "%:p"
    copen
    let &makeprg     = mp
    let &errorformat = ef
endfunc
nmap <leader>li :call PylintCheck()<CR>

" map rc :call CompileRunGpp()<CR>
func! CompileRunGpp()
    exec "w"
    exec "!gcc % -o %<"
    exec "! %<"
endfunc

func! RunResult()
    let mp = &makeprg
    let ef = &errorformat
    " let exeFile = expand("%:t")
    let exeFile = expand("%:t")
    exec "w"
    if &ft == "Python"
        setlocal makeprg=python\ -u
    elseif &ft == "perl"
        setlocal makeprg=perl
    elseif &ft == "ruby"
        setlocal makeprg=ruby
    elseif &ft == "autohotkey"
        setlocal makeprg=AutoHotkey
    endif
    " let com = make . ' "' . expand("%") . '"'
    " silent execute com
    silent make %
    " silent make expand("%")
    if &ft != "autohotkey"
        set efm=%C\ %.%#,%A\ \ File\ \"%f\"\\,\ line\ %l%.%#,%Z%[%^\ ]%\\@=%m
        copen
    endif
    let &makeprg     = mp
    let &errorformat = ef
endfunc
map <F5> :call RunResult()<CR>

" Automatic Pair
" inoremap ( ()<ESC>i
" inoremap { {<CR>}<ESC>ko
inoremap <buffer> ( <c-r>=AutoPair('(')<CR>
inoremap <buffer> ) <c-r>=ClosePair(')')<CR>
inoremap <buffer> [ <c-r>=AutoPair('[')<CR>
inoremap <buffer> ] <c-r>=ClosePair(']')<CR>
inoremap <buffer> { <c-r>=AutoPair('{')<CR>
inoremap <buffer> } <c-r>=ClosePair('}')<CR>
inoremap <buffer> % <c-r>=AutoPair('%')<CR>

func! AutoPair(char)
    if "(" == a:char
        if &ft =~ '^\(sql\)$'
            return "(\<Enter>);\<Up>\<Enter>"
        elseif '' == getline('.')[col('.')]
            return "()\<Left>"
        else
            return a:char
        endif
    elseif "[" == a:char
        if '' == getline('.')[col('.')]
            return "[]\<Left>"
        else
            return a:char
        endif
    elseif "{" == a:char
        if &ft =~ '^\(ruby\|java\|perl\)$'
            return "{\<Enter>}\<ESC>ko"
            " return "{}\<ESC>i\<Enter>"
        elseif '' == getline('.')[col('.')] && &ft =~ '^\(python\|autohotkey\|vim\|snippet\)$'
            return "{}\<Left>"
        else
            return a:char
        endif
    elseif "%" == a:char
        if '' == getline('.')[col('.')] && &ft =~ '^\(autohotkey\)$'
            return "%%\<left>"
        else
            return a:char
        endif
    endif
endfunc

func! ClosePair(char)
    if getline('.')[col('.') - 1] == a:char
        return "\<Right>"
    else
        return a:char
    endif
endfunc

inoremap <buffer> <BS> <c-r>=DeletePairs()<CR>
inoremap <buffer> <C-h> <c-r>=DeletePairs()<CR>

" Delete the pair of parentheses, brackets and braces
function! DeletePairs()
    let AutoPaires = {')':'(',']':'[','}':'{'}
    if has_key(AutoPaires, getline('.')[col('.') - 1]) && getline('.')[col('.') - 2 ] == AutoPaires[getline('.')[col('.') - 1]]
        return "\<BS>\<DEL>"
    else
        return "\<BS>"
    endif
endfunction

function! GotoFile()
    if 'python' == &ft
        let com = expand('%:p:h') . '\' . expand('<cfile>') . '.py'
    " elseif 'ruby' == &ft
        " let com = expand('%:p:h') . '\' . expand('<cfile>')
        " let com = 'C:\Marslo\Job\Summa\TEX\SVN\netact-mpp-lab-scripts\' . expand('<cfile>')
    else
        let com = expand('<cfile>')
    endif
    silent execute ':e ' . com
    echo 'Open file "' . com . '" under the cursor'
endfunction
nmap gf :call GotoFile()<CR>

" Update tags file automatic
function! UpdateTags()
    silent !ctags -R --fields=+ianS --extra=+q
endfunction
nmap <F12> :call UpdateTags()<CR>

" Reduce the font
function! FontSize_Reduce()
    if has('unix')
        let pattern = '\<\d\+$'
    elseif has('win32') || has('win95') || has('win64')
        let pattern = ':h\zs\d\+\ze'
        " let pattern = '\([1-9][0-9]*\)$'
    endif
    let fontsize = matchstr(&gfn, pattern)
    let cmd = substitute(&gfn, pattern, string(fontsize - 1), 'g')
    let &gfn=cmd
endfunction
nnoremap <A--> :call FontSize_Reduce()<CR>

" Enlarge the font
function! FontSize_Enlarge()
    if has('unix')
        let pattern = '\<\d\+$'
    elseif has('win32') || has('win95') || has('win64')
        let pattern = ':h\zs\d\+\ze'
        " let pattern = '\([1-9][0-9]*\)$'
    endif
    let fontsize = matchstr(&gfn, pattern)
    let cmd = substitute(&gfn, pattern, string(fontsize + 1), 'g')
    let &gfn=cmd
endfunction
nnoremap <A-+> :call FontSize_Enlarge()<CR>

iabbrev <leader>/* /*********************************
iabbrev <leader>*/ *********************************/
iabbrev <leader>#- #------------------
inoremap <leader>tt <c-r>=strftime("%d/%m/%y %H:%M:%S")<cr>
inoremap <leader>fn <C-R>=expand("%:t:r")<CR>
inoremap <leader>fe <C-R>=expand("%:t")<CR>

" Moveing shortcut under the Insert mode
inoremap <M-b> <Esc>Bi
inoremap <M-f> <Esc>f<Space>a
inoremap <M-d> <Esc>cf<Space>

" Disable <F1> to open help file
nmap <F1> <ESC>
imap <F1> <ESC>

" Set Ctrl-A/Ctrl-E jump to the Begin-of-line/End-of-line
map <C-a> <ESC>^
imap <C-a> <ESC>I
map <C-e> <ESC>$
imap <C-e> <ESC>A

" Set the Ctrl-K/Ctrl-J switch the Top/Bottom window
map <C-k> <C-w>k
map <C-j> <C-w>j

" map ecq :echo system("ruby -e 'print $:.join(%q{,})'")<CR>

" CtrlP
" let g:ctrlp_map = '<s-w>'
let g:ctrlp_max_height = 8
let g:ctrlp_max_depth = 100
let g:ctrlp_working_path_mode = '.ctrlp'
" let g:ctrlp_user_command = 'dir %s /-n /b /s /a-d'
let g:ctrlp_working_path_mode = 'ra'
let g:ctrlp_custom_ignore = {
\ 'dir':  '\v[\/]\.(git|hg|svn)$',
\ 'file': '\v\.(exe|so|dll)$',
\ }

" GundoToggle
nnoremap <leader>g :GundoToggle<CR>
let g:gundo_width = 35
let g:gundo_preview_height = 20
let g:gundo_right = 0
let g:gundo_preview_bottom = 0


" ====================================== For Inteface =====================================
" Make vim maximize while it startup
func! MaximizeWindow()
    silent !wmctrl -r :ACTIVE: -b add,maximized_vert,maximized_horz
endfunc

" The initialize size
set lines=40
set columns=122

if has('win32') || has('win95') || has('win64')
    " Max after start vim
    " au GUIEnter * simalt ~x
    " autoload _vimrc
    autocmd! bufwritepost $VIM/_vimrc source %
    " Fast Edit vim configuration
    nmap <leader>v :e $VIM/_vimrc<CR>
    " Fonts
    set guifont=Monaco:h12
    " Copy the content to system clipboard by using y/p
    set clipboard+=unnamed
    set clipboard+=unnamedplus
else
    " au GUIEnter * call MaximizeWindow()
    autocmd! bufwritepost ~/.vimrc source %
    nmap <leader>v :e ~/.vimrc<CR>
    set guifont=Monaco\ 12
    set clipboard=unnamedplus
endif

set nobackup
set noswapfile
set nowritebackup

set number
colorscheme desert_Marslo_ForLinux_v4
" colorscheme neverness
let psc_style='cool'

syntax enable
syntax on
filetype plugin on

" Hide everything
" set go=
" Hide the tool bar
set guioptions-=T
" Hide the side scroll bar
set guioptions-=r
" Add the bottom scroll bar
" set guioptions+=b
" Hide the Menu
set guioptions-=m
set cpoptions+=n
" Set auto read when a file is changed by outside
set autoread
" Show matching bracets
set showmatch

" Wrap lines
set nowrap
" Tab width
set tabstop=4
set expandtab
" set textwidth=150
set autoindent
set smartindent
" the width of <Tab> in first line would refer to 'Shiftwidth' parameter
set smarttab
" the width while trigger <Tab> key
set softtabstop=4
" the tab width by using >> & <<
set shiftwidth=4
set lbr
set tw=0

" Show Line and colum number
set ruler

" Set status bar
set laststatus=2
set statusline=%m%r
set statusline=%f\ \ %y,%{&fileformat}\  " file path\file name & filetype
set statusline+=%=      " right align
set statusline+=\ \ %-{strftime(\"%H:%M\ %d/%m/%Y\")}   " Current Time
set statusline+=\ \ %b[A],0x%B    " ASCII code, Hex mode
set statusline+=\ \ %c%V,%l/%L      " current Column, current Line/All Line
set statusline+=\ \ %p%%\

" At Menu
source $VIMRUNTIME/delmenu.vim
source $VIMRUNTIME/menu.vim
" At Console
language messages utf-8

" Show (partial) command in status line
set showcmd

" Fold
set foldenable "Enable Fold
set foldmethod=manual
set foldcolumn=1
set foldexpr=1 "Shown line number after fold
set foldlevel=100 " Not fold while VIM set up
" au BufWinLeave * silent mkview
autocmd BufWinLeave * silent! mkview
autocmd BufWinEnter * silent! loadview
set viewoptions=folds

" Highlight the txt file
au BufRead,BufNewFile * setfiletype txt
" au BufRead,BufNewFile *txt setfiletype txt
" Perl test file as Perl code
au BufRead,BufNewFile *.t set ft=perl

" MiniBufExpl Colors
hi MBEVisibleActive guifg=#5DC2D6 guibg=#333333
hi MBEVisibleNormal guifg=#A6DB29 guibg=#333333

" ====================================== For Function =====================================
" make backspace h, l, etc wrap to
set backspace=indent,eol,start
" set whichwrap+=<,>,h,l

" nmap <leader>s :ConqueTermSplit
if has('win32') || has('win95') || has('win64')
    nmap tv :ConqueTermSplit cmd <CR>
else
    nmap tv :ConqueTermSplit bash <CR>
endif

" Delete the black space in the end of each line
nmap zdb :%s/\s\+$//<CR>
nmap zhh :%s/^\s\+//<CR>
nmap zmm :g/^/ s//\=line('.').' '/<CR>
" Delete white space
nmap zws :g/^\s*$/d<CR>

" Search
set incsearch
set hlsearch
set ignorecase
set smartcase

" Regular Expression
set magic

" WinManager
" let g:AutoOpenWinManager=1
let g:winManagerWidth = 20
let g:winManagerWindowLayout='FileExplorer|TagList'
nmap mm :WMToggle<cr>

" Tlist
let Tlist_Show_One_File=1
let Tlist_Exit_OnlyWindow=1
let Tlist_Use_SingleClick=1
let Tlist_File_Fold_Auto_Close=1
let Tlist_GainFocus_On_ToggleOpen=1
let Tlist_show_Menu=1
let Tlist_sql_settings = 'sql;P:package;t:table'
let Tlist_Process_File_Always=0
" let Tlist_Close_On_Select=1
" let Tlist_Auto_Open=1
" let Tlist_Ctags_Cmd='x:\xx\xx\ctags.exe'
" let updatetime=1000
nmap tl :TlistToggle<CR>

" Tagbar
map <F3> :TagbarToggle<CR>
let g:tagbar_left=1
let g:tagbar_width=20
let g:tagbar_autofocus=1
let g:tagbar_expand=1
let g:tagbar_singleclick=1
let g:tagbar_iconchars=['+', '-']
let g:tagbar_autoshowtag=1

" MiniBufExplorer
let g:miniBufExplMapCTabSwitchBufs=1
let g:miniBufExplMapWindowNavArrows=1
let g:miniBufExplorerMoreThanOne=0 " Mult-files openning at one minibufexplorer
let g:miniBufExplMapWindowNavVim=1 " Using <HJKL> to change the buffer
let g:miniBufExplTabWra=1
let g:miniBufExplModSelTarget=1     " Use other explorers like TagList set it at 1
let g:miniBufExplUseSingleClick=1 " Single Click could switch the buffers

" Comments
" nmap <silent> ,x
" vmap <silent> <C-Q> ,x
" imap <silent> <C-Q> ,x
let g:EnhCommentifyAlignRight='yes'
let g:EnhCommentifyRespectIndent='yes'
let g:EnhCommentifyPretty='Yes'
let g:EnhCommentifyMultiPartBlocks='yes'
let g:EnhCommentifyUseSyntax='Yes'
let g:EnhCommentifyCallbackExists='Yes'


" AutoInfo
map <F4> :AuthorInfoDetect<cr>
let g:vimrc_author='Marslo'
let g:vimrc_email='marslo.vida@gmail.com'

" MRU
" let MRU_File=$VIM.'\Data\mru_files.txt'
let MRU_Auto_Close=1
map <silent> <leader>h :MRU<CR>
map <C-g> :MRU<CR>
let MRU_Max_Entries=10

" Perl extended
let perl_extended_vars=1           " Highlight funny variables correctly
let perl_include_pod=1             " Pod highlighting
let perl_fold=1                    " Fold Perl constructs by default
let perl_nofold_packages=1         " Not packages, though
let perl_want_scope_in_variables=1 " Highlight variable scope distinctly

" perl-support.vim
let g:Perl_Root='&Plugin.&Perl.'
let g:Perl_LoadMenus='yes'

let g:Perl_AuthorName='Marslo'
let g:Perl_Email='marslo.vida@gmail.com'
" let tlist_perl_settings='perl;c:constants;l:;abels;s:subroutines;d:POD'

set linespace=0
set wildmenu
set wildignore+=*.swp,*.zip,*.exe

" The blinding frequency
" set mat=2

" turn off error beep/flash
set noerrorbells
set novisualbell
set t_vb=

set completeopt=longest,menu

" Cursor format
set guicursor=a:hor6
set guicursor+=i-r-ci-cr-o:hor6-blinkon0

set cursorline
hi LineNr guifg=#555555 guibg=background ctermfg=darkgrey ctermbg=none
hi CursorLineNr guifg=#A6E22E guibg=background gui=NONE ctermbg=black ctermfg=lightgreen
" hi CursorLine   cterm=NONE ctermbg=darkgrey ctermfg=white guibg=darkgrey guifg=white

" IndentLine
let g:indentLine_color_gui = '#282828'
let g:indentLine_color_term = 8
let g:indentLine_indentLevel = 20
let g:indentLine_showFirstIndentLevel = 1
let g:indentLine_char = '¦'
" let g:indentLine_loaded = 1

set list listchars=tab:\ \ ,trail:·,extends:»,precedes:«,nbsp:·
